plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.openapi.generator' version '7.5.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

sourceSets.main.java.srcDirs = ['build/generated-source/src/main/java', 'src/main/java']

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.postgresql:postgresql:42.7.3'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.apache.camel.springboot:camel-spring-boot-starter:4.5.0'
	implementation 'org.apache.camel.springboot:camel-kafka-starter:4.1.0'
	implementation 'org.liquibase:liquibase-core'
	implementation "org.openapitools:jackson-databind-nullable:0.2.1"
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
	implementation group: 'jakarta.validation', name: 'jakarta.validation-api', version: '3.0.2'
	implementation "com.fasterxml.jackson.core:jackson-core:${jackson_version}"
	implementation "com.fasterxml.jackson.core:jackson-annotations:${jackson_version}"
	implementation "com.fasterxml.jackson.core:jackson-databind:${jackson_version}"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jackson_version}"

	compileOnly 'jakarta.servlet:jakarta.servlet-api:5.0.0'
	implementation "io.github.openfeign:feign-jackson:${feign_version}"
	implementation "io.swagger:swagger-annotations:${swagger_annotations_version}"

	compileOnly 'org.projectlombok:lombok'
	testImplementation 'org.projectlombok:lombok'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

//https://openapi-generator.tech/docs/generators/spring/ parameters
tasks.create("openApiGenerateAsync", org.openapitools.generator.gradle.plugin.tasks.GenerateTask.class, {
	generatorName.set("spring")
	templateDir = "$projectDir/src/main/resources/openapi/templates"
	inputSpec.set("$rootDir/specs/kafka.events.openapi.yml")
	outputDir.set("$buildDir/generated-source")
	apiPackage.set("com.example.deposit.async.api")
	invokerPackage.set("com.example.deposit.async.invoker")
	modelPackage.set("com.example.deposit.async.model")
	configOptions.set([
			dateLibrary                     : "java8",
			java8                           : "true",
			useBeanValidation               : "true",
			interfaceOnly                   : "true",
			useOptional                     : "false",
			useLombok                       : "true",
			"useSpringBoot3"                : "true",
	])
})

tasks.create("openApiGenerateRest", org.openapitools.generator.gradle.plugin.tasks.GenerateTask.class, {
	generatorName.set("spring")
	inputSpec.set("$rootDir/specs/rest.openapi.yml")
	outputDir.set("$buildDir/generated-source")
	templateDir = "$projectDir/src/main/resources/openapi/templates"
	apiPackage.set("com.example.deposit.rest.api")
	invokerPackage.set("com.example.deposit.rest.invoker")
	modelPackage.set("com.example.deposit.rest.model")
	configOptions.set([
			dateLibrary                     : "java8",
			java8                           : "true",
			useBeanValidation               : "true",
			interfaceOnly                   : "true",
			useOptional                     : "false",
			useLombok                       : "true",
			"useSpringBoot3"                : "true",
	])
})

compileJava.dependsOn 'openApiGenerateAsync', 'openApiGenerateRest'